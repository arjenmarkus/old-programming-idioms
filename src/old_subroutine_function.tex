\section{Subroutines and functions}
Older compilers were not so very sophisticated when it comes to checking the
actual argument lists of calls to subroutines or functions against the
dummy (expected) argument lists of these program units. This is partly due
to there not being a mechanism in FORTRAN for providing such checks, even though
some compilers could implement these checks at run-time. It was also used sometimes
to allow for flexibility. For instance: if you do not care if an array holds integers
or reals, because you are simply writing them to some binary file, then one
routine would suffice to take care of both types (perhaps even including double
precision numbers).

NOTE: example

A more common situation: allow one-dimensional arrays to be used as two-dimensional
or pass scalars to array dummy arguments. As long as you remain with the allotted
memory, there should be no problem.

NOTE: example

\subsection{Passing arrays}
\label{passingarrays}
Several surprising idioms are connected to the passing of arrays to subroutines
and functions:
\begin{itemize}
\item
The dummy argument could be declared as \verb+REAL ARRAY(1)+. This was not
unusual in programs predating FORTRAN~77, as that introduced the asterisk for
arrays whose sizes are not defined in the routine perse: \verb+REAL ARRAY(*)+, the
so-called assumed-size arrays. A small example:
\begin{verbatim}
      SUBROUTINE PRINT( A, N )
      INTEGER N
C
C     Array declared pre FORTRAN 77:
C
      REAL A(1)
C
C     Array declared the FORTRAN 77 way:
C
C     REAL A(*)
      ...
      END
\end{verbatim}

In both cases, the routine would have to know what the actual size of the array
should be. Often, that was done via an extra argument like the argument~\verb+N+
in the example (see for a more elaborate and practical example section \ref{workarrays}).

With Fortran you can use assumed-shape arrays, so that the dimensions of the array
are passed on automatically. This does require that the interface is explicit. If the
compiler does not see that interface, it has to assume that the FORTRAN~77 style is
to be applied.

\item
The actual argument could be some element of the array:
\begin{verbatim}
      REAL ARRAY(100)

      CALL PRINT( ARRAY(10), 30 )
      END

      SUBROUTINE PRINT( A, N )
      INTEGER N
      REAL A(N)
      WRITE(*,*) A
      END
\end{verbatim}

The call to \verb+PRINT+ passes the array elements 10, 11, ... of \verb+ARRAY+ to the subroutine
and in the subroutine it is assumed that the array \verb+A+ (its first dummy argument)
is \verb+N+ (it second dummy argument) elements large. Then we can print the dummy array
as a whole.
\end{itemize}

With Fortran we can specify a section of an array, not necessarily a contiguous section:
\begin{verbatim}
real :: array(100)

call print( array(10::10) )
\end{verbatim}

The array section selects elements 10, 20, ... 100, but as the subroutine \verb+PRINT+
is in old-style FORTRAN, it does not get an \emph{array descriptor}. The compiler
will have to make a temporary array, copy in the selected elements, call \verb+PRINT+
and before releasing that temporary array, copy the elements of that temporary
array into the selected elements. \emph{It does not know the intent of the arguments
of the subroutine} \verb+PRINT+ \emph{after all!}

Marking the \emph{intent} of arguments has at least two advantages:
documenting the intended use of the argument and making certain optimisations
possible. Like in the above example: copying back is not necessary if the argument
is \verb+intent(in)+.


\subsection{Specific names for intrinsic functions}
With the advent of FORTRAN 77 the language gained generic names of such intrinsic
functions as \verb+MAX+ and \verb+SIN+. Pre-FORTRAN~77 code uses specific names
to distinguish the types of the arguments and the result. Thus:
\begin{itemize}
\item
The minimum and maximum functions for integers were \verb+MIN0+ and \verb+MAX0+.
\item
The minimum and maximum functions for single-precision reals were \verb+AMIN1+ and \verb+AMAX1+.
\item
The minimum and maximum functions for double-precision reals were \verb+DMIN1+ and \verb+DMAX1+.
\item
If you wanted an \emph{integer} result from a list of \emph{reals}, then the function names
were \verb+MIN1+ and \verb+MAX1+. But a \emph{real} result from a list of \emph{integers}
is returned by \verb+AMIN0+ and \verb+AMAX0+.
\item
For the trigonometric functions: \verb+SIN+, \verb+DSIN+ and \verb+CSIN+ would return
the sine of respectively a single-precision real, a double-precision real and a complex value.
\end{itemize}

The Internet, as usual, will provide detailed information on the various intrinsic functions.

Sometimes, these specific names are required if you want to pass them as arguments to a
routine. The best way is to avoid doing so and write a small helper function instead,
as these specific names are deprecated and may even be removed from the standard altogether.

\subsubsection{A curious function: DIM}
Here is a function that is defined by the FORTRAN~77 standard but whose usefulness
seems questionable -- at least I have never actually seen it used:
%
\begin{verbatim}
C     If X > Y, then return the difference, otherwise zero.
      POSDIF = DIM(X,Y)
\end{verbatim}

This is merely to warn that other functions and subroutines may be found
in old FORTRAN code that have lost their meaning or have been replaced by others.
Compiler-specific procedures that have been replaced by standard facilities include
\verb+SYSTEM+ to run an external program and various functions to get the system time.


\subsection{The EXTERNAL statement}
Consider the following program:
%
\begin{verbatim}
      PROGRAM USEFNC
      REAL A
      n = 10
      WRITE(*,*) A(N)

      END
\end{verbatim}

You cannot build it by itself, because it refers to a \emph{function} \verb+A+ which is external
to the program. You get an error about an unresolved symbol or a similar message.
If you add the definition of a function \verb+A+ to the build step, then, of course,
that symbol is resolved and the program can be built.

But the more important thing to notice is that the call to \verb+A+ is indistinguishable
from using \verb+A+ as an array. To make the distinction clear, you can use the
\verb+EXTERNAL+ statement. This is also useful for arguments you pass that are actually
procedures (see \verb+integral.f+):
%
\begin{verbatim}
      PROGRAM CALC
      REAL A, INTGRL
      EXTERNAL A, INTGRL

      XBEGIN = 0.0
      XEND   = 1.0
      WRITE(*,*) INTGRL(A, XBEGIN, XEND)
      END

      FUNCTION A(X)
      REAL X
      A = X ** 2
      END

      REAL FUNCTION INTGRL(A, XB, XE)
      REAL A, XB, XE
      EXTERNAL A
      REAL DX

      DX     = (XE - XB) / 10.0
      INTGRL = 0.0
      DO 100 I = 1,10
          X      = XBEGIN + (I-1) * DX
          INTGRL = INTGRL + A(X)
  100 CONTINUE

      INTGRL = INTGRL * DX
      END
\end{verbatim}
%
Note that the declaration of \verb+INTGRL+ as a \emph{real} function is required
because the implicit typing rules would otherwise make it an integer, with very
strange results.

NOTE: EXTERNAL for an intrinsic procedure


\subsection{Statement functions}
Short functions like the function \verb+A+ in the previous section can also be
written as so-called \emph{statement functions}. These appear among the general
declarations:
\begin{verbatim}
      PROGRAM CALC
      REAL INTGRL
C
C Use a statement function instead of a regular function
C
      A(X) = X ** 2

      XBEGIN = 0.0
      XEND   = 1.0

      INTGRL = 0.0

      DO 100 I = 1,10
          X      = XBEGIN + (I-1) * DX
          INTGRL = INTGRL + A(X)
  100 CONTINUE

      INTGRL = INTGRL * DX
      WRITE(*,*) INTGRL(A, XBEGIN, XEND)
      END
\end{verbatim}

As a statement function is not allowed as an actual argument, the integration
is now moved into the program itself instead of being implemented in a separate
function.

Statement functions can be useful to abbreviate expressions that appear at
multiple locations in the code of a routine, but \emph{internal procedures}
in Fortran are much more useful and recognisable.


\subsection{Constant values as actual arguments}
A peculiar phenomenon was possible in the old days: you could change the
value of a literal constant. That is: if you were sloppy, you could change the
value of, say, \verb+10+ into \verb+20+:
\begin{verbatim}
      PROGRAM CONST

      CALL DOUBLE( 10 )
      WRITE(*,*) 10
      END

      SUBROUTINE DOUBLE( X )
      INTEGER X

      X = 2 * X
      END
\end{verbatim}

Nowadays, compilers will not pass the literal constant itself but a copy or something similar, so that
the above example will do no harm. But if the implementation is such that \verb+10+
is actually a memory address that is readable and writeable, then havoc is raised with, as
a bonus, difficult to track bugs.


\subsection{The ENTRY statement}
FORTRAN offered the \verb+ENTRY+ statement to provide alternative entries into a subroutine or function.
Such statement makes it possible to call the procedure with a different argument list, for instance.
A slightly contrived example:
\begin{verbatim}
      REAL FUNCTION SETACC( X, START )
      REAL X, START

      REAL SUM
      SAVE SUM

      SUM    = START + X
      SETACC = SUM
      RETURN

      ENTRY NXTACC( X )
      REAL X

C
C     Accumulate to the variable SUM defined in SETACC
C
      SUM    = SUM + X
      NXTACC = SUM
      END
\end{verbatim}

With the funcion \verb+SETACC+ you initialise an "accumulator" variable and with \verb+NXTACC+
you add a new value to it and get the result. In a program you could use it like this (see \verb+accum.f+):
\begin{verbatim}
      PROGRAM ACCUM
      REAL X, SUM

      OPEN( 10, FILE = 'accum.inp' )

      READ( 10, * ) X
      SUM = SETACC( X, 0.0 )

  100 CONTINUE
      READ( 10, *, END = 110, ERR = 110 ) X
      SUM = NXTACC( X )
      GOTO 100

  110 CONTINUE
      WRITE(*,*) 'Sum: ', SUM
      END
\end{verbatim}

There are doubtless other uses for \verb+ENTRY+, but the Fortran standard offers modules and module
variables for this sort of things instead.

As the \verb+ENTRY+ statement only defines the name and the argument list, it serves as a subroutine,
if its corresponding program unit is a subroutine or as a function of the same type. Its argument list
can, however, differ.


\subsection{Alternative returns}
A deprecated feature in Fortran, alternative returns provide a means to
transfer the program control from a routine to its caller to another
statement than the one following. This feature could be used for
error handling (see \verb+errors.f+):
\begin{verbatim}
      PROGRAM ERRORS
      REAL X

      X = 2.0
      CALL PRSQRT( X, *900 )

      X = -1.0
      CALL PRSQRT( X, *900 )
      STOP
C
C     Handle any errors
C
  900 CONTINUE
      WRITE(*,*) 'Routine PRSQRT expects a positive argument'

      END

      SUBROUTINE PRSQRT( X, * )
      REAL X

      IF ( X .GE. 0.0 ) THEN
          WRITE(*,*) 'The square root of ', X, ' is ', SQRT(X)
      ELSE
          RETURN 1
      ENDIF
      END
\end{verbatim}

Note: because the feature is deprecated (obsolescent), compilers may not properly check that
the actual call has the right number of statement labels in the interface. In most cases
a simple return code works quite as well (the section \ref{jumpingtoend} offers more
on the subject).

