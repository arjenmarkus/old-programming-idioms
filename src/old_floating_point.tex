\section{Floating-point numbers}
Nowadays, most computers use the IEEE format for representing floating-point numbers.
The two main types you will encounter are single-precision reals, occupying four
bytes of memory, and double-precision reals, occupying eight bytes. Fortran of old,
including FORTRAN, favours single-precision -- without any decoration a literal
number like \verb+1.23456789+ is single-precision, whereas many other languages
use double-precision reals by default.

Back in the days before the IEEE standard was widely adopted \cite{IEEE}, reals were
represented in many different ways and also the arithmetic operations we normally take
for granted were not fully portable. This led to all manner of complications if
you wanted to make your program portable, and that was and is certainly a goal of Fortran.

\subsection{REAL*4 and REAL*8}
One of the many extensions that were added by compiler vendors was the use of an asterisk~(*)
to indicate the precision:
\begin{verbatim}
*
*     Single precision real
*
      REAL*4 X
*
*     DOUBLE PRECISION REAL
*
      REAL*8 Y
*
*     Single precision complex
*
      COMPLEX*8 Z
\end{verbatim}
The number indicates the number of bytes that a real would occupy. This has never been
part of a FORTRAN or Fortran standard. The \emph{kind} feature in Fortran is much more
flexible, as it can capture aspects of the representation of floating-point numbers
beyond mere storage.\footnote{The current standard defines a general model for representing
real numbers. This encompasses the IEEE formats, but is in fact more general.}

While FORTRAN has supported complex numbers for a very long time, it did not standardise
double-precision complex numbers. Compiler extensions like \verb+COMPLEX*16+ often
filled that gap. With Fortran, the \emph{kind} feature is used to achieve this:
\begin{verbatim}
integer, parameter :: dp = kind(1.d0)
complex(kind=dp)   :: z
\end{verbatim}

The kind refers to the underlying floating-point number, not the storage, like with
the \verb+COMPLEX*16+ type.

\begin{quote}
\emph{Note:} Such notation is sometimes used for integers and logicals as well. Again
the \emph{kind} feature is much more useful than merely indicating the storage size.
\end{quote}

\begin{quote}
\emph{Note:} I have used a Convex computer in the distant past that actually had two
different types of floating-point numbers that both were single-precision. One was
structured according to the IEEE standard and the other was a native format. The difference
for all intents and purposes was the interpretation of the exponent. The native format
was said to be a bit faster, but they occupied the same storage, four bytes.

For the same bit pattern the \emph{values} differed by a factor~4.
\end{quote}

\subsection{Literals in the source code}
One thing to keep in mind: if a literal number occurs in the source code, it is interpreted
as it appears, independent of the context. For Fortran this has been standardised: an expression
on the right-hand side is evaluated independently of the left-hand side. More concretely:
\begin{verbatim}
    double precision pi = 3.14159265358979323846264338327950288419716939937510
\end{verbatim}
\noindent may look to specify $\pi$ in some 50 decimals, but to the compiler it is
merely a slightly bizarre way of expressing it in \emph{single-precision}, so actually
only six or seven significant decimals. To get \emph{double-precision}, you need to
add a \emph{kind} or, as it was in FORTRAN, a "d" exponent (with some excess decimals removed):
\begin{verbatim}
    double precision pi = 3.141592653589793238462643d0
\end{verbatim}

You can see the difference if you run this program:
\begin{verbatim}
program diff_double_precision
    implicit none

    double precision, parameter :: pi_1 = 3.141592653589793238462643
    double precision, parameter :: pi_2 = 3.141592653589793238462643d0

    write(*,*) 'Difference: ', pi_1 - pi_2
end program diff_double_precision
\end{verbatim}
\noindent which prints (you may expect slight differences in the last few decimals with
different compilers):
\begin{verbatim}
 Difference:    8.7422780126189537E-008
\end{verbatim}

Some old FORTRAN compilers seem to have been less strict about the dichotomy between
the left-hand side and the right-hand side and would indeed interpret such literal
numbers as double-precision.

Another thing to keep in mind is that many compilers, both new and old, allow for
compiler options that turn the \emph{default} precision for a variable declared as \verb+real+
into \emph{double precision}. If a program relies on this behaviour, then you need to
carefully check the code.\footnote{In general, using these compile options is
considered a bad idea. It is all too easy to forget them when building the program or library.}


\subsection{Retrieving properties of the floating-point arithmetic}
Quite often in robust numerical algorithms, like calculating special functions (Bessel, gamma, ...), it is necessary
to know the properties of the floating-point system the program uses, such as the smallest
or the largest value that can be represented. In Fortran there are various intrinsic functions
that allow this information to be queried, but in FORTRAN that was not standardised. Also,
the floating-point systems differed a great deal more than they do nowadays. To solve that,
functions called \verb+R1MACH+, \verb+D1MACH+ and even \verb+I1MACH+ were used that returned
the required information. They sometimes had to be adapted to the machine you were working
on, as indicated in the comments of \verb+R1MACH+:

\begin{verbatim}
      REAL FUNCTION R1MACH(I)
      INTEGER I
C
C  SINGLE-PRECISION MACHINE CONSTANTS
C  R1MACH(1) = B**(EMIN-1), THE SMALLEST POSITIVE MAGNITUDE.
C  R1MACH(2) = B**EMAX*(1 - B**(-T)), THE LARGEST MAGNITUDE.
C  R1MACH(3) = B**(-T), THE SMALLEST RELATIVE SPACING.
C  R1MACH(4) = B**(1-T), THE LARGEST RELATIVE SPACING.
C  R1MACH(5) = LOG10(B)
C
      INTEGER SMALL(2)
      INTEGER LARGE(2)
      INTEGER RIGHT(2)
      INTEGER DIVER(2)
      INTEGER LOG10(2)
C     needs to be (2) for AUTODOUBLE, HARRIS SLASH 6, ...
      INTEGER SC
      SAVE SMALL, LARGE, RIGHT, DIVER, LOG10, SC
      REAL RMACH(5)
      EQUIVALENCE (RMACH(1),SMALL(1))
      EQUIVALENCE (RMACH(2),LARGE(1))
      EQUIVALENCE (RMACH(3),RIGHT(1))
      EQUIVALENCE (RMACH(4),DIVER(1))
      EQUIVALENCE (RMACH(5),LOG10(1))
      INTEGER J, K, L, T3E(3)
      DATA T3E(1) / 9777664 /
      DATA T3E(2) / 5323660 /
      DATA T3E(3) / 46980 /
C  THIS VERSION ADAPTS AUTOMATICALLY TO MOST CURRENT MACHINES,
C  INCLUDING AUTO-DOUBLE COMPILERS.
C  TO COMPILE ON OLDER MACHINES, ADD A C IN COLUMN 1
C  ON THE NEXT LINE
      DATA SC/0/
C  AND REMOVE THE C FROM COLUMN 1 IN ONE OF THE SECTIONS BELOW.
C  CONSTANTS FOR EVEN OLDER MACHINES CAN BE OBTAINED BY
C          mail netlib@research.bell-labs.com
C          send old1mach from blas
C  PLEASE SEND CORRECTIONS TO dmg OR ehg@bell-labs.com.
C
C     MACHINE CONSTANTS FOR THE HONEYWELL DPS 8/70 SERIES.
C      DATA RMACH(1) / O402400000000 /
C      DATA RMACH(2) / O376777777777 /
C      DATA RMACH(3) / O714400000000 /
C      DATA RMACH(4) / O716400000000 /
C      DATA RMACH(5) / O776464202324 /, SC/987/
       ...
\end{verbatim}

These functions were carefully constructed as were the algorithms that used
them. In this day and age, almost all such variation in the properties of
floating-point numbers has been eliminated via the adoption of the IEEE standard.
That does not mean of course that floating-point arithmetic holds no surprises
anymore. (One classic text on the subject is


\subsection{Input in the absence of a decimal point}
Disk storage nowadays is all but endless, but this luxury did not exist in the old days.
This may have been the reason for a little known or used feature in the input of real numbers:
if a string representing a real number does not contain a decimal point, then the \emph{input~format}
may insert it.

Here is an example, using internal I/O to make it self-contained (see also the file \verb+input_no_point.f90+):
\begin{verbatim}
program show_insert_point
    implicit none

    real :: x
    character(len=10) :: string

    string = '1234'

    read( string, '(f4.0)' ) x
    write(*,*) x

    read( string, '(f4.2)' ) x
    write(*,*) x
end program show_insert_point
\end{verbatim}
It produces:
\begin{verbatim}
   1234.00000
   12.3400002
\end{verbatim}
With the format in the second read statement a decimal point is inserted!

It may have been useful in the past but it does suggest that to avoid surprises, you better not
use input format with a prescribed number of decimals.


