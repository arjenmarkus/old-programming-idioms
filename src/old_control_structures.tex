\section{Control structures}
FORTRAN came with a small number of control constructs and it was quite usual to construct
other control flows via \verb+IF+ and \verb+GOTO+ statements. It inherited some constructs
from its predecessors that are very uncommon nowadays: the arithmetic (or three-way) \verb+IF+ and the
computed \verb+GOTO+, as well as the \verb+ASSIGN+ statement. This part of the document
highlights these ancient idioms.

\subsection{Ordinary and nested DO-loops}
The ordinary \verb+DO+ loop in FORTRAN looks like this:
%
\begin{verbatim}
      DO 110 I = 1,10
          ... do something useful ...
  110 CONTINUE
\end{verbatim}
%
The statement label \verb+110+ indicates the end of the \verb+DO+ loop and anything in between
is repeatedly executed. The Fortran equivalent is, unsurprisingly:
%
\begin{verbatim}
do i = 1,10
    ... do something useful ...
enddo
\end{verbatim}
%
But there are a few more things to say about these \verb+DO+ loops. First of all, the
statement label needs not appear with a \verb+CONTINUE+ statement. It could very well be
put on the last executable statement:
%
\begin{verbatim}
      SUM = 0.0
      DO 110 I = 1,10
  110     SUM = SUM + ARRAY(I)
\end{verbatim}
%
It can even be used for multiple, nested, \verb+DO+ loops:
%
\begin{verbatim}
      SUM = 0.0
      DO 110 J = 1,10
      DO 110 I = 1,10
  110     SUM = SUM + ARRAY(I,J)
\end{verbatim}
%
To skip a part of the calculation, you can use a \verb+GOTO+ statement, where in Fortran
you would use a \verb+cycle+ or \verb+exit+ statement:
%
\begin{verbatim}
*
* Sum the positive elements only and only if the sum
* remains smaller than 1.0
*
      SUM = 0.0
      DO 110 I = 1,10
          IF ( ARRAY(I) .LE. 0.0 ) GOTO 110
          IF ( SUM .GT. 1.0 ) GOTO 120
          SUM = SUM + ARRAY(I)
  110 CONTINUE
  120 CONTINUE
\end{verbatim}
%
The example is a little contrived, so that you can see the use of the \verb+GOTO+ statement
for both \verb+cycle+ and \verb+exit+. The modern equivalent becomes:
%
\begin{verbatim}
!
! Sum the positive elements only and only if the sum
! remains smaller than 1.0
!
sum = 0.0
do i = 1,10
    if ( array(i) <= 0.0 ) cycle
    if ( sum > 1.0 ) exit
    sum = sum + array(i)
enddo
\end{verbatim}
%
Note that sharing statement labels in a nested \verb+DO+ loop makes it difficult to
see what a statement \verb+GOTO endlabel+ should mean: skip an iteration or skip
the rest of the inner \verb+DO+ loop:
%
\begin{verbatim}
      SUM = 0.0
      DO 110 J = 1,10
      DO 110 I = 1,10
          IF ( SUM .GT. 1.0 ) GOTO 110
  110     SUM = SUM + ARRAY(I,J)
\end{verbatim}
%
In FORTRAN 66 (also known as FORTRAN IV) there was a significant difference with
the \verb+DO+ loop you find in current Fortran: a \verb+DO+ loop would always
be run at least once! This is due to the location of the check on the iteration
condition, whether it is put at the start or at the end of the \verb+DO+ loop.
Many compilers still provide an option to allow for the FORTRAN 66 semantics,\cite{Intel}
which includes this feature:\footnote{I could not find such a flag for the
gfortran compiler, but for Intel Fortran oneAPI it is -f66.}

NOTE: revise this text!
%
\begin{verbatim}
* With the right compiler options, print this line once!
* (Note: FORTRAN 77, not strictly FORTRAN 66)
      DO 110 I = 1,0
          WRITE(*,*) 'FORTRAN 66: ', I
  110 CONTINUE
      WRITE(*,*) 'Current value of i:', i
\end{verbatim}
%
With FORTRAN 66 semantics the sample program (see \verb+f66_loop.f90+) prints:
%
\begin{verbatim}
 FORTRAN 66:            1
 Current value of i:           2
\end{verbatim}
With modern semantics it prints:
%
\begin{verbatim}
 Current value of i:           1
\end{verbatim}
%
This can result in subtle but nasty differences, if you are unaware of what
was meant!


\subsection{Simulating a DO-WHILE loop}
There was no explicit \verb+DO WHILE+ construct in FORTRAN, at least not
in the standard. Therefore you would need to simulate it using any of the
following methods:

\vspace{\baselineskip}
\noindent \emph{A} \verb+DO+ \emph{loop with a large upper bound:}
\begin{verbatim}
* Find the right line in a file
      DO 110 I = 1,10000000
          READ( 10, '(A)' ) LINE
          IF ( LINE(1:1) .NE. '*' ) THEN
              GOTO 120
          ENDIF
  110 CONTINUE
  120 CONTINUE
* Found the start of the information, proceed
      ...
\end{verbatim}

\vspace{\baselineskip}
\noindent \emph{A combination of statement labels and} \verb+GOTO+ \emph{-- check at the start:}
\begin{verbatim}
* Find the right line in a file
      READ( 10, '(A)' ) LINE
  110 CONTINUE
      IF ( LINE(1:1) .NE. '*' ) GOTO 120
      READ( 10, '(A)' ) LINE
      GOTO 110
  120 CONTINUE

* Found the start of the information, proceed
      ...
\end{verbatim}
\noindent (This example is a bit artificial to keep it in line with the other two,
but similar constructs with different processing definitely occur in practice!)

\vspace{\baselineskip}
\noindent \emph{A combination of statement labels and} \verb+GOTO+ \emph{-- check at the end:}
\begin{verbatim}
* Find the right line in a file
  110 CONTINUE
      READ( 10, '(A)' ) LINE
      IF ( LINE(1:1) .EQ. '*' ) GOTO 110

* Found the start of the information, proceed
      ...
\end{verbatim}

A modern equivalent would either use the \verb+DO WHILE+ loop or the unlimited
\verb+DO+ loop:
%
\begin{verbatim}
!
! Find the right line in a file
!
read( 10, '(a)' ) line
do while (line(1:1) == '*' )
    read( 10, '(a)' ) line
enddo

!
! Found the start of the information, proceed
!
...
\end{verbatim}
%
Or:
%
\begin{verbatim}
!
! Find the right line in a file
!
do
    read( 10, '(a)' ) line
    if (line(1:1) == '*' ) exit
enddo

!
! Found the start of the information, proceed
!
...
\end{verbatim}

The precise location of the check on the condition depends on what the purpose is and
whether you can actually check it at the start of the loop, as with a \verb+DO WHILE+,
or whether you require some preliminary calculation first. If you want to convert
old-style source code, beware that the logic may sometimes have to be reverted,
particularly if the condition comes at the end of the loop.

\emph{Note for self: https://www.intel.com/content/www/us/en/docs/fortran-compiler/developer-guide-reference/2023-0/f66.html}


\subsection{Three-way IF statements and computed GOTOs}
Two types of statements that are quite alien to what you find in modern-day programming
languages are the three-way or artihmetic \verb+IF+ statement and the computed \verb+GOTO+ statement.
The latter could be used to simulate a \verb+select case+ construct, the first on the
other hand was, in modern eyes, an unusual predecessor of the \verb+IF ... ELSE ... ENDIF+
block.

A \emph{computed} \verb+GOTO+ \emph{statement} takes a list of statement labels and a single integer expression:
%
\begin{verbatim}
      GOTO (100, 200, 300) JMP
  100 CONTINUE
      WRITE(*,*) 'Jump: 1'
      GOTO 400
  200 CONTINUE
      WRITE(*,*) 'Jump: 2'
      GOTO 400
  300 CONTINUE
      WRITE(*,*) 'Jump: 3'
  400 CONTINUE
      ... the rest ...
\end{verbatim}
%
Depending on the value of this expression (the value of \verb+JMP+ in the above example,
the control would jump to the Nth label. If the value was zero or lower, the \verb+GOTO+
would not be executed and the program control would simply continue with the next statement.
This is the case too with a value that is larger than the number of statement labels.
(See as an illustration the source file \verb+computed_goto.f90+)

Since there is nothing special about the statement labels the control would jump to, you had
to make sure to jump somewhere else after the handling of each case. In the example that
is done by jumping to label 400.

The \verb+select case+ construct of Fortran is better behaved, as you do not have to
take care of jumping to the end yourself and it is possible to select the case via strings
as well as integer values or even ranges.

There is nothing particularly magic about the \emph{three-way} \verb+IF+ \emph{statement}.
But you need to know how it works:
%
\begin{verbatim}
      IF (IVALUE) 100, 200, 300
  100 CONTINUE
      WRITE(*,*) 'Value is negative - ', IVALUE
      GOTO 400
  200 CONTINUE
      WRITE(*,*) 'Value is zero - ', IVALUE
      GOTO 400
  300 CONTINUE
      WRITE(*,*) 'Value is positive - ', IVALUE
  400 CONTINUE
      ... the rest ...
\end{verbatim}
%
The action, a jump to one of the three statement labels, to be taken depends on the
\emph{sign} of the integer expression. Often two of the statement labels would be the same,
as two possibilities are more common than three. To see it in action, see the source
file \verb+three_way_if.f90+. Both statement types still exist in Fortran, or at least
in the compilers, to support old-style programs.\footnote{The arithmetic IF statement
was deleted from the Fortran 2018 standard, as gfortran will report. Intel Fortran
will tell you this when you specify the standard as f18 -- "-stand", defaulting to the
Fortran 2018 standard.}


\subsection{Jumping to the end}
\label{jumpingtoend}
The \verb+GOTO+ statement was and is also used to jump to a completely different
part of the program unit for reporting error conditions:
%
\begin{verbatim}
      SUBROUTINE PRSQRT( X )

      IF ( X .LT. 0.0 ) GOTO 900

      WRITE(*,*) 'Square root of X = ', X, ' is ', SQRT(X)
      RETURN

900   CONTINUE
      WRITE(*,*) 'X should not be negative - ', X
      STOP
      END
\end{verbatim}

Such statements can be gathered at the end of the program unit so as not to clotter
the code that deals with normal processing. If you want to avoid the \verb+GOTO+ statement,
then the modern \verb+BLOCK+ construct will help:
%
\begin{verbatim}
subroutine print_sqrt( x )
    real :: x


    normal: block
        if ( x < 0.0 ) exit normal

        write(*,*) 'square root of x = ', x, ' is ', sqrt(x)
        return
    end block normal
    !
    ! Error processing
    !
    errors: block
        write(*,*) 'x should not be negative - ', x
        stop
    end block errors

end subroutine print_sqrt
\end{verbatim}

\emph{Note:} the \verb+errors+ block is merely introduced to syntactically
distinguish the error handling from the normal handling. It does not influence
in this form the flow of control.


\emph{Note to self: http://www.u.arizona.edu/\~rubinson/copyright\_violations/Go\_To\_Considered\_Harmful.html
Reprinted from Communications of the ACM, Vol. 11, No. 3, March 1968, pp. 147-148. Copyright (c) 1968, Association for Computing Machinery, Inc.
}


\subsection{The ASSIGN statement}
The uses of \verb+GOTO+ so far have all been static: the statement labels were fixed
in the code. While the \verb+GOTO+ statement is frowned upon and it can certainly make
the control flow difficult to follow when you do not use it in an orderly fashion, there
is a possibility to use "dynamic" labels, so that the \verb+GOTO+ effectively jumps to
a varying location. This is achieved by the \verb+ASSIGN+ statement. It is not often
used, as in most cases better and especially clearer constructs are possible, even
in FORTRAN, but here is one possible case:

\noindent Suppose you have to compute something complicated in a number of places in a
program unit, based on a large number of variables, so that using a subroutine or
a function is awkward, as it leads to a very long argument list. Nowadays you
can easily use an internal routine, but this was not the case with FORTRAN.
So, with the \verb+ASSIGN+ statement you could store a location to return to,
jump ahead to the complicated piece of code, jump back when done, and remain in
the same routine. Here is a simple example:
%
\begin{verbatim}
      SUBROUTINE( ICASE )
      A = 1.0
      B = 2.0
      C = 3.0
      IF ( ICASE .EQ. 1 ) ASSIGN 100 TO JMP
      IF ( ICASE .EQ. 2 ) ASSIGN 200 TO JMP
      IF ( ICASE .EQ. 3 ) ASSIGN 300 TO JMP
      GOTO 900

* Case 1: use the result in F
  100 CONTINUE
      WRITE(*,*) 'Case ', ICASE, 'value is ', F
      GOTO 400

* Case 2: use the result in G
  200 CONTINUE
      C = 4.0
      WRITE(*,*) 'Case ', ICASE, 'value is ', G
      GOTO 400

* Case 3: use the result in H
  300 CONTINUE
      WRITE(*,*) 'Case ', ICASE, 'value is ', H
      GOTO 400

* All done, continue
  400 CONTINUE
      WRITE(*,*) 'Done'
      RETURN

*
  900 CONTINUE
* We can use the local variables directly
      F = A + B + C
      G = A + B * C
      H = A * B + C

* We are done, so return to the "caller"
      GOTO JMP
      END
\end{verbatim}

It is a useless and contrived example, but it is only meant to illustrate how
a "dynamic" jump can be constructed -- the part after statement label 900
is actually independent of whatever happens above it. You can extend it with
new cases, without having to worry about the computational part.

\subsubsection{The DO-loop with a real index variable}
A peculiar feature of FORTRAN 77 was the introduction of a DO-loop gouverned
by a real variable. This means that the precise values of the variable might
influnce the number of iterations, for instance. A simple program to
illustrate this (see \verb+real_do.f90+):
\begin{verbatim}
      PROGRAM REAL_DO
      IMPLICIT NONE

      INTEGER I, N
      REAL    R

      DO 120 I = 1,10
          N = 0
          DO 110 R = 0.0, 1.0*I, 0.1*I
              WRITE(*,*) R
              N = N + 1
          ENDDO
          WRITE(*,*) 'Number of iterations: ', N
      ENDDO
      END
\end{verbatim}

The output of the program with the Intel Fortran (leaving out the values of \verb+R+):
\begin{verbatim}
 Number of iterations:           11
 Number of iterations:           11
 Number of iterations:           10
 Number of iterations:           11
 Number of iterations:           11
 Number of iterations:           10
 Number of iterations:           11
 Number of iterations:           11
 Number of iterations:           10
 Number of iterations:           11
\end{verbatim}

So, some DO-loops get 11 iterations, what you would expect with exact arithmetic
and others get only 10. If you run this program with the gfortran compiler the result
is a uniform 11 iterations -- apparently the loop is controlled in a different way!
Apart from the unpredictability of the number of iterations with one compiler, you
can also not rely on the portability of the program.
