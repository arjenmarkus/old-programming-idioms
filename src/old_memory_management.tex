\section{Memory management}

In the decades leading up to the FORTRAN 77 standard, memory management was simple:
declare what memory you need via statically sized arrays and that is it. There was
no dynamic memory allocation, at least not in the FORTRAN language. It may surprise
you, but even the concept of an operating system that took care of the computer
was fairly new, as illustrated by a 1971 book by D.W. Barron, titled "Computer
Operating Systems". Quoting from the book's jacket:
\begin{quote}
As the operating system is becoming an important part of the software complex
accompanying a computer system. A large amount of knowledge about the subject
now exists, mainly in the form of papers in computer journals. It is thus time for
a book that coordinates what is known about operating systems.
\end{quote}

There are, however, a few aspects of FORTRAN that make the story a bit more
complicated: \verb+COMMON+ blocks, \verb+EQUIVALENCE+ and the \verb+SAVE+ statement.
All three will be discussed here.


\subsection{COMMON blocks}
Variables, be they scalars or arrays, are normally passed via argument lists between
program units (the main program, subroutines or functions). This is the immediately
visible part. But you can also pass variables via \verb+COMMON+ blocks. These
constitute a form of global \emph{memory}, but not of global \emph{variables}, as
a \verb+COMMON+ block merely allocates memory and the mapping of memory
locations onto variables is up to the program units themselves. For instance:
\begin{verbatim}
      SUBROUTINE SUB1
      COMMON /ABC/ X(10)
      ...
      END

      SUBROUTINE SUB2
      COMMON /ABC/ A(5), B(5)
      ...
      END
\end{verbatim}
The \verb+COMMON+ block \verb+/ABC/+ appears in two subroutines, but in subroutine
\verb+SUB1+ it is associated with the array \verb+X+ of 10 elements and in the
subroutine \verb+SUB2+ it is associated with two arrays, \verb+A+ and \verb+B+,
both having five elements. The memory is shared, so that if you set \verb+X(1)+
to, say, \verb+1.1+ in subroutine \verb+SUB1+, then on the next call to subroutine
\verb+SUB2+, the array element \verb+A(1)+ will have that same value, as they occupy
the same memory location.

\verb+COMMON+ blocks should have the same \emph{size} in all locations in the program's
code where they occur. That is difficult to ensure, hence it was common (no pun intended)
to put the declaration of \verb+COMMON+ blocks in so-called include files. Each program
unit that needed to address the memory allocated via these \verb+COMMON+ blocks could
then use the \verb+INCLUDE+ statement to have the compiler insert the literal text
of that include file.\footnote{The INCLUDE statement was actually a common compiler
extension.}

There are various ways that \verb+COMMON+ blocks were used:
\begin{itemize}
\item
Variables in \verb+COMMON+ blocks are persistent. At least, that was a very common
occurrence. The rules in the FORTRAN standard are more complicated, but certainly
with the \verb+SAVE+ statement you can rely on these variables to retain the
values between calls to a routine.
\item
Often routines in a library have to cooperate: one routine is used to set options
and other routines do the actual work. By using one or more \verb+COMMON+ blocks
these options do not need to be passed around via the argument list.
\item
Together with \verb+EQUIVALENCE+ statements you could use the \verb+COMMON+ blocks
to share workspace. Remember: back in the days memory was much and much more precious
and scarcer than it is now. So, defining work arrays \verb+WORK+ (of type real)
and \verb+IWORK+ (of type integer) and making them equivalent to each other, you
could save on memory, if these arrays are not used at the same time.

In the code that would look like:
\begin{verbatim}
      COMMON /ABC/ WORK(1000)
      EQUIVALENCE (WORK(1), IWORK(1))
\end{verbatim}
\noindent with a typical sloppiness with respect to array dimensions.

\end{itemize}
Nowadays, it is much easier to pass large amounts of essentially private data
around, simply define a suitable derived type. Also, it is easy to allocate
work arrays as you require them and release them again when done.

A special \verb+COMMON+ block was the so-called \emph{blank} \verb+COMMON+
block. It had no name and it did not have to be declared with the same size
in all parts of the program. In fact, on some systems it could be used as
a flexible reservoir of memory, in much the same way as you have the
heap nowadays. But this particular use was an extension to the standard.


\subsubsection{More on EQUIVALENCE}
A specific use of the \verb+EQUIVALENCE+ statement is to access
the binary representation of a (floating-point) number. A concrete
example is the conversion of big-endian numbers to little-endian
numbers or vice versa:
%
\begin{verbatim}
      INTEGER      A, B
      CHARACTER*4  BIG, LITTLE
      EQUIVALENCE (A, BIG)
      EQUIVALENCE (B, LITTLE)
C
C     Read some number from a file - big endian
C     and rearrange the bytes
C
      READ(10) A
      LITTLE(1:1) = BIG(4:4)
      LITTLE(2:2) = BIG(3:3)
      LITTLE(3:3) = BIG(2:2)
      LITTLE(4:4) = BIG(1:1)

      WRITE(*,*) 'Value is: ', B
\end{verbatim}

With Fortran, however, you can use the \verb+transfer()+ function to
convert the integer to a sequence of four individual characters or
simply use the bit manipulation functions to extract the information.


\subsection{The SAVE statement}
According to the FORTRAN standard a local variable in a function or
subroutine does not retain its value between calls, unless it has the
\verb+SAVE+ attribute:
%
\begin{verbatim}
      SUBROUTINE ACCUM( ADD )
*
*     Accumulate the counts
*
      INTEGER ADD

      INTEGER TOTAL
      SAVE    TOTAL
      DATA    TOTAL / 0 /

      TOTAL = TOTAL + ADD
      IF ( TOTAL > 100 ) THEN
          WRITE(*,*) 'Reached: ', TOTAL
      ENDIF
      END
\end{verbatim}

However, some implementations, notably on DOS/Windows, used static
storage for these local variables, which meant that the variables would
\emph{seemingly} retain their values, even without the \verb+SAVE+ statement.
If a program relied on this property and was ported to a different environment,
all manner of havoc could be raised.

\begin{quote}
\emph{Note:} I have actually had lively, but not necessarily pleasant, debates on
whether the behaviour either way was correct. Sometimes the unexpected
behaviour was claimed to indicate a compiler bug.

Some compilers have an option to enforce the \verb+SAVE+ attributes on variables,
irrespective of the source code. You should take special care if old source code
relies on such an option.
\end{quote}

\subsection{The initial values of (local) variables}
A feature related in a way to the \verb+SAVE+ statement is the fact that
in both FORTRAN and Fortran variables do not get a particular initial
value, unless they have the \verb+SAVE+ attribute, implicitly or explicitly.
With older compilers local variables may be stored in static memory and
quite often they may have an initial value of zero or whatever the
equivalent is for the variable's type, but that is in all cases simply
a random circumstance. \emph{Never assume that a variable that has not
been explicity given a value, has a particular value.}

You can set the initial value in FORTRAN via the \verb+DATA+ statement:
%
\begin{verbatim}
      LOGICAL FIRST
      DATA FIRST / .TRUE. /
\end{verbatim}

This means that at the first call to the subroutine holding this variable
\verb+FIRST+, it has the value \verb+.TRUE.+. You can later set it to
\verb+.FALSE.+ to indicate that the subroutine has been called at least once
before, so that no initialisation is needed anymore:
%
\begin{verbatim}
* Subroutine that sums the values we pass
      SUBROUTINE SUM( x )
      INTEGER X

      INTEGER TOTAL
      LOGICAL FIRST
      DATA FIRST / .TRUE. /

      IF ( FIRST ) THEN
          FIRST = .FALSE.
          TOTAL = 0
      ENDIF

      TOTAL = TOTAL + X
      END
\end{verbatim}
(Just a variation on the previous example). This is not a very interesting routine,
but it illustrates a typical use.

\emph{To emphasize:} This type of initialisation is done so that the variables
in question have the designated value at the first call. If you change
the value, then they retain that new value. No reinitialisation occurs.
(Actually, the value is not set on the first call, but rather is part
of the data section of the program as a whole. There is no separate
assignment.)

The \verb+DATA+ statement is not executable, it normally appears
somewhere in the section that defines the variables, but it may occur
elsewhere -- most FORTRAN and Fortran compilers are not strict about it.

There is some peculiar syntax involved:
%
\begin{verbatim}
      REAL X(100)
      DATA X / 1.0, 98*0.0, 100.0 /
\end{verbatim}

You can repeat values in a somewhat similar way as with edit descriptors
in format statements: a count followed by an asterisk (*) and the value
to be repeated. It is also possible to use implied do-loops:
%
\begin{verbatim}
      INTEGER I
      REAL X(100)

      DATA (X(I), I = 1,100) / 100*1.0 /
\end{verbatim}

While it is more usual to set the values together with the declaration
of a variable nowadays, like:
%
\begin{verbatim}
  integer :: i
  real    :: x(100) = [ (1.0, i = 1,size(x)) ]
\end{verbatim}
\noindent the \verb+DATA+ statement is more versatile, because it is
not necessary to set the values for an array in one single statement:
%
\begin{verbatim}
    integer :: i
    real    :: x(100)

    data (x(i), i = 1,50)   / 50*1.0 /
    data (x(i), i = 51,100) / 50*0.0 /
\end{verbatim}

So, this old-fashioned statement may have its uses still.

Another peculiarity: the \verb+DATA+ statement has effect on the
size of the object file and thus the executable itself. The following
program leads to an executable of approximately 5.7 MB using gfortran
on Windows:
\begin{verbatim}
program data_stmt
    implicit none

    integer :: i
    real    :: array(1000000)

    data (array(i), i = 1,size(array),2) / 500000*1.0 /
    data (array(i), i = 2,size(array),2) / 500000*2.0 /

    !
    ! Alternative
    !
    !! array(1::2) = 1.0
    !! array(2::2) = 2.0
    !

    write(*,*) sum(array)
end program data_stmt
\end{verbatim}

If, instead, you use the alternative and remove the \verb+DATA+
statements, the executable is only 1.7 MB. Of course, this is
an exaggerated example, but it illustrates that such \verb+DATA+
statements are very different in character than ordinary, executable
statements.


\subsection{Initialising variables in COMMON blocks: BLOCK DATA}
\label{blockdata}
The \verb+DATA+ statement plays an important role when it comes to initialising
variables in a \verb+COMMON+ block. Since the \verb+COMMON+ blocks usually
appear in more than one subprogram (main program, subroutines, functions),
they cannot be initialised in the same way as ordinary variables: which
\verb+DATA+ statement should prevail, if several initialise the same
\verb+COMMON+ variables?

Thus enter the \verb+BLOCK DATA+ program unit!

It is the only way to initialise variables in a \verb+COMMON+ block and
it is special, because it is not executable and is not part of a routine
or the main program. The peculiar consequence is
that you cannot put it in a library: there is no reference to it, unlike
with subroutines and functions, so it would never be loaded. Instead, you
will normally put in the same file as the main program or link against
its object file explicitly.

The general layout is:
%
\begin{verbatim}
      BLOCK DATA
      ... COMMON blocks ...
      ... DATA statements ...
      END
\end{verbatim}

Here is a small example of this effect:\footnote{I use the gfortran compiler
to illustrate such effects, but it would be similar with other compilers.
And since most if not all FORTRAN features are still supported in Fortran,
I also use free-form sources.}
%
\begin{verbatim}
gfortran -o common1 common.f90 block.f90
gfortran -o common2 common.f90
\end{verbatim}

Both build commands succeed, despite the fact that one part of the program is missing
in the second one.

The \verb+block.f90+ source file is:
%
\begin{verbatim}
BLOCK DATA
COMMON /ABC/ X
DATA X /42/
END
\end{verbatim}

The \verb+common.f90+ source file is:
%
\begin{verbatim}
PROGRAM PRINTX
COMMON /ABC/ X
WRITE(*,*) 'Expected value of X = 42:'
WRITE(*,*) 'X = ', X
END
\end{verbatim}

Program \verb+common1+ prints the value 42, whereas the other
program prints 0. If the \verb+BLOCK DATA+ program unit had been
an ordinary program unit, the building of this version would have failed
on an unresolved symbol or the like.


\subsection{Work arrays}
\label{workarrays}
In the old days you could encounter arguments to a routine that represented
such workspace. Usually you would have to declare the arrays to a size that
matches the problem at hand. Here is an example from the \emph{LAPACK} library
for linear algebra:
%
\begin{verbatim}
      SUBROUTINE DGELS( TRANS, M, N, NRHS, A, LDA, B, LDB, WORK, LWORK,
     $                  INFO )
*
*  -- LAPACK driver routine (version 3.2) --
*  -- LAPACK is a software package provided by Univ. of Tennessee,    --
*  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
*     November 2006
*
*     .. Scalar Arguments ..
      CHARACTER          TRANS
      INTEGER            INFO, LDA, LDB, LWORK, M, N, NRHS
*     ..
*     .. Array Arguments ..
      DOUBLE PRECISION   A( LDA, * ), B( LDB, * ), WORK( * )
*     ..
\end{verbatim}

In this case, the argument \verb+WORK+ is a double-precision array of
size \verb+LWORK+. In the comments that document the use of this routine
the precise usage is described:
%
\begin{verbatim}
*  WORK    (workspace/output) DOUBLE PRECISION array, dimension (MAX(1,LWORK))
*          On exit, if INFO = 0, WORK(1) returns the optimal LWORK.
*
*  LWORK   (input) INTEGER
*          The dimension of the array WORK.
*          LWORK >= max( 1, MN + max( MN, NRHS ) ).
*          For optimal performance,
*          LWORK >= max( 1, MN + max( MN, NRHS )*NB ).
*          where MN = min(M,N) and NB is the optimum block size.
*
*          If LWORK = -1, then a workspace query is assumed; the routine
*          only calculates the optimal size of the WORK array, returns
*          this value as the first entry of the WORK array, and no error
*          message related to LWORK is issued by XERBLA.
\end{verbatim}

This means that for a particular case you can either use one of the formulae
or the special value \verb+-1+ for \verb+LWORK+ to obtain an optimal value.
The work array itself would still be a statically declared array.

Note that with the current features of Fortran the interface could be greatly
simplified:\footnote{Intentionally left in fixed form.}
%
\begin{verbatim}
      SUBROUTINE DGELS( TRANS, A, B, INFO )
*
*     .. Scalar Arguments ..
      CHARACTER, INTENT(IN) :: TRANS
      INTEGER, INTENT(OUT)  :: INFO
*     ..
*     .. Array Arguments ..
      DOUBLE PRECISION, INTENT(INOUT) ::  A(:,:), B(:,:)
*     ..
\end{verbatim}
%
\noindent provided the interface is made explicit via a module or an interface block.

\begin{quote}
The careful reader wil note that one feature of the original interface
has not been retained in the simplification: \verb+NHRS+. Thus, with this
revised interface the array \verb+B+ should consist entirely of right-hand
side vectors.
\end{quote}
