\section{Handling characters}
With the FORTRAN 77 character strings were introduced. Before that there was very little
that could be done with characters, unless they represented numbers. It was the era
of the hollerith constants. But FORTRAN~77 only introduced fixed-length strings.
Only with the advent of Fortran~2003 did the language acquire the possibility to
work with strings of dynamically allocatable lengths.


\subsection{Hollerith constants}
The main use you are likely to encounter of hollerith constants is in format statements or
as constant strings passed to a subroutine. Here is
a code fragment from a library for special functions, like the Bessel functions and
many others:
\begin{verbatim}
c
      if (ntk0.ne.0) go to 10
      ntk0 = inits (bk0cs, 11, 0.1*r1mach(3))
      xsml = sqrt (4.0*r1mach(3))
      xmax = -log(r1mach(1))
      xmax = xmax - 0.5*xmax*log(xmax)/(xmax+0.5) - 0.01
c
 10   if (x.le.0.) call seteru (29hbesk0   x is zero or negative, 29,
     1  2, 2)
      if (x.gt.2.) go to 20
\end{verbatim}

The construction \verb+29hbesk0   x is zero or negative+ is a hollerith constant, in
this case, it is 29 characters long and passed to a subroutine that will print an
error message. The signature of the subroutine is:
\begin{verbatim}
      subroutine seteru (messg, nmessg, nerr, iopt)
      common /cseter/ iunflo
      integer messg(1)
      data iunflo / 0 /
\end{verbatim}

So the hollerith string is stored in an integer array \verb+messg+, but note:
\begin{itemize}
\item
The size of the array is defined as 1, because there was no concept of an assumed-size array
before FORTRAN~77 (see section \ref{passingarrays}).
\item
The variable \verb+iunfo+ is apparently initialised in an ordinary \verb+DATA+ statement,
even though it is contained in a \verb+COMMON+ block (see section \ref{blockdata}).
\item
The code is written in lower-case. Strictly speaking that is not conforming to the standard.
\item
The array \verb+messg+ in turn is passed onto another routine, \verb+seterr+, but the code
for that routine simple prints "Error!" and we are not shown how to deal with the integer
array that holds the character string.
\end{itemize}

Modernising holleriths that only serve as static strings is simple: turn them into ordinary
strings. However, sometimes they were used to manipulate string data and then you need
to understand exactly what is going on, especially because vendor-specific methods were
typically used and the storage of hollerith constants was non-portable \cite{hollerith}.

\emph{Note to self: https://en.wikipedia.org/wiki/Hollerith\_constant}

An example of manipulating character strings pre-FORTRAN~77 is given here, shamelessly
copied from a Fortran discourse thread. The code fragment uses compiler-specific
statements \verb+DECODE+ and \verb+ENCODE+ to move characters to and from integers:
\begin{verbatim}
      CHARACTER S*6 / '987654' /, T*6
      INTEGER V(3)*4
      DECODE( 6, '(3I2)', S ) V
      WRITE( *, '(3I3)') V
      ENCODE( 6, '(3I2)', T ) V(3), V(2), V(1)
      PRINT *, T
      END
\end{verbatim}

As stated in the thread:
\begin{quote}
The above program has this output:

\begin{verbatim}
98 76 54
547698
\end{verbatim}

The DECODE reads the characters of S as 3 integers, and stores them into V(1), V(2), and V(3).
The ENCODE statement writes the values V(3), V(2), and V(1) into T as characters; T then contains '547698'.
\end{quote}


\emph{Note to self: https://fortran-lang.discourse.group/t/code-that-baffles-me-could-someone-please-explain/6005}


\subsection{Fixed-length character arguments}
Just as with arrays you specify a fixed length for character arguments. The argument will act as if it actually
holds strings of the specified length, but other than with arrays, a mismatch with the actual argument may lead
to strange results. Here is a small example:
\begin{verbatim}
C234567
      PROGRAM SHWSTR
      CHARACTER*10 STRING(5)

      STRING(1) = 'ABCDEFGHIJ'
      STRING(2) = '1234567890'
      STRING(3) = '?         '
      STRING(4) = '?         '

      CALL STR3(  STRING, 2 )
      CALL STR13( STRING, 2 )
      END
      SUBROUTINE STR3( STRING, N )
      CHARACTER*3 STRING(N)
      INTEGER I
      DO I = 1,N
          WRITE(*,*) I, STRING(I)
      ENDDO
      END
      SUBROUTINE STR13( STRING, N )
      CHARACTER*13 STRING(N)
      INTEGER I
      DO I = 1,N
          WRITE(*,*) I, STRING(I)
      ENDDO
      END
\end{verbatim}

The output of the program is:
\begin{verbatim}
           1 ABC
           2 DEF
           1 ABCDEFGHIJ123
           2 4567890?
\end{verbatim}

Since character strings are laid out in memory in a contiguous fashion, the mismatch causes the
strings as seen in the subroutines to be just strings of the given length, \emph{independent therefore}
of the strings in the calling program unit.

In general, it is best to use assumed-length strings: \verb+CHARACTER*(*) STRING+ or, as Fortran
would have it: \verb+character(len=*) :: string+.

\subsection{Interfacing to C: hidden arguments}
Since a subroutine or function that has one or more character arguments must somehow "know" the
length of the actual arguments (otherwise the assumed-length declaration would not work),
that information is passed from the caller to the callee. The method is not prescribed by the
standard, but there exist at least two methods, which are both invisible from the FORTRAN side.
But if you interface with C routines, then it becomes important to know which one is actually
used: on the C side a hidden argument that gives the length is visible and this extra argument
is either directly after the character argument or after all the regular arguments.

This is one of the difficulties that existed in FORTRAN in dealing with other languages, notably~C.

Here is an illustration. The program calls a C routine that accepts a character string array,
much as in the previous section:
\begin{verbatim}
C234567
      PROGRAM SHWSTR
      CHARACTER*10 STRING(5)

      STRING(1) = 'ABCDEFGHIJ'
      STRING(2) = '1234567890'
      STRING(3) = '?         '
      STRING(4) = '?         '

      CALL STRC(  STRING, 2 )
      END
\end{verbatim}

The C routine does very little, but looks like this (the declaration is more important than
the body), if the hidden argument comes right after the string argument:
\begin{verbatim}
/* Hidden argument directly after the associated argument */
#define STRC ...
void STRC( char *string, int lenstr, int *n ) {
    ...
}
\end{verbatim}

\noindent and otherwise like this:

\begin{verbatim}
/* Hidden argument after all ordinary arguments */
#define STRC ...
void STRC( char *string, int *n, int lenstr ) {
    ...
}
\end{verbatim}

Note that:
\begin{itemize}
\item
We define the name via a macro, \verb+STRC+, because the actual name depends on the FORTRAN compiler.
With Fortran 2003, this problem is solved via the \verb+bind(C)+ attribute, with or without the \verb+name = "..."+ clause.
\item
The hidden argument is a \emph{value}, not a pointer like the size parameter $n$. In FORTRAN such arguments
cannot be expressed, so that more often than not the interfacing with C requires an intermediate "wrapper" routine
to translate such incompatibilities. Again, Fortran~2003 solves much of this in a standardized way.
\end{itemize}
